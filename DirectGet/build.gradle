def thisGroup    = 'directget'
def thisArtifact = 'get'
def thisVersion  = '0.15.5-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group   = thisGroup
version = thisVersion

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Dependencies

repositories {
    mavenLocal()
    jcenter()
    maven {
        url 'https://raw.githubusercontent.com/dssb/maven-repository/master/'
    }
}

dependencies {
    compile 'dssb-callerid:dssb-callerid-impl:1.2.0'
    compile 'dssb-failable:dssb-failable-api:1.0.0'
    compile 'dssb-utils:dssb-utils-common:1.0.4'
    
    compile     'org.projectlombok:lombok:1.16.16'
    testCompile 'junit:junit:4.12'
}


// IDE

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}
idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

// Building/Publishing

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            
            artifact sourceJar
            artifact packageJavadoc
        }
    }
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath
    
    options {
        setMemberLevel JavadocMemberLevel.PUBLIC
        setAuthor true
        
        links "https://docs.oracle.com/javase/8/docs/api/"
    }
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task packageJavadoc(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}
gradle.projectsEvaluated {
    tasks.withType(JavaCompile) { options.compilerArgs << "-parameters" }
}
